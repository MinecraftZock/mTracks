import org.gradle.internal.jvm.Jvm

buildscript {
    ext.kotlin_version = "2.1.21"

    repositories {
        google()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.10.1"
        classpath "com.github.triplet.gradle:play-publisher:3.12.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:4.4.2"
        classpath "com.google.firebase:firebase-crashlytics-gradle:3.0.3"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.3.0"
    }
}

plugins {
    // ./gradlew dependencyUpdates -Drevision=release
    id "com.github.ben-manes.versions" version "0.52.0"
    id "com.google.devtools.ksp" version "2.1.21-2.0.1" apply false
}

println "Gradle uses Java ${Jvm.current()}"

ext {
    defaultCompileSdkVersion = 35
    defaultMinSdkVersion = 23
    defaultTargetSdkVersion = 35
    koin_version = "4.0.4"
    gsonVersion = "2.13.1"
    defaultPlayLibVersion = "21.3.0"
    room = "2.7.1"
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint" // Version should be inherited from parent

    // Optionally configure plugin
    ktlint {
        debug = false
        android = true
        verbose = true
        outputColorName.set("RED")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url = "https://jitpack.io" }
    }

    // show detailed warnings in gradle output
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }

    configurations.configureEach {
        resolutionStrategy {
            force("com.google.auto.value:auto-value-annotations:1.11.0")
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

